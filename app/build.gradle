plugins {
    id "io.freefair.lombok" version "8.0.1"
    id "run.halo.plugin.devtools" version "0.1.1"
    id 'java'
}

group 'run.halo.links'
version = rootProject.version

jar {
    archiveBaseName.set("plugin-links")
    dependsOn(":api:jar")
}

dependencies {
    implementation project(':api')
    compileOnly 'run.halo.app:api'

    testImplementation 'run.halo.app:api'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

tasks.register('copyUI', Copy) {
    dependsOn ':ui:build'
    from project(':ui').layout.buildDirectory.dir('dist')
    into layout.buildDirectory.dir('resources/main/console')
}

tasks.named('processResources') {
    dependsOn('copyUI')
}

test {
    useJUnitPlatform()
}

halo {
    version = '2.20'
    debug = true
}

haloPlugin {
    openApi {
        outputDir = file("$rootDir/api-docs/openapi/v3_0")
        groupingRules {
            linksV1alpha1Api {
                displayName = 'Extension API for Links'
                pathsToMatch = [
                    '/apis/core.halo.run/v1alpha1/links/**',
                    '/apis/core.halo.run/v1alpha1/linkgroups/**',
                    '/apis/api.plugin.halo.run/v1alpha1/plugins/PluginLinks/**'
                ]
            }
        }
        groupedApiMappings = [
                '/v3/api-docs/linksV1alpha1Api': 'linksV1alpha1Api.json'
        ]
        generator {
            outputDir = file("${rootDir}/ui/src/api/generated")
        }
    }
}
