plugins {
    id "com.github.node-gradle.node" version "7.1.0"
    id "run.halo.plugin.devtools" version "0.6.2"
    id "io.freefair.lombok" version "9.1.0"
    id 'java'
}

group 'run.halo.links'

repositories {
    mavenCentral()
}

dependencies {
    implementation platform('run.halo.tools.platform:plugin:2.22.5')
    compileOnly 'run.halo.app:api'

    testImplementation 'run.halo.app:api'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.release = 21
}

node {
    nodeProjectDir = file("${project.projectDir}/console")
}

tasks.register('buildFrontend', PnpmTask) {
    args = ['build']
    dependsOn('installDepsForUI')
}

tasks.register('installDepsForUI', PnpmTask) {
    args = ['install']
}

build {
    // build frontend before build
    tasks.named('compileJava').configure {
        dependsOn('buildFrontend')
    }
}

halo {
    version = '2.22.5'
    debug = true
}

haloPlugin {
    openApi {
        outputDir = file("$rootDir/api-docs/openapi/v3_0")
        groupingRules {
            linksV1alpha1Api {
                displayName = 'Extension API for Links'
                pathsToMatch = [
                    '/apis/core.halo.run/v1alpha1/links/**',
                    '/apis/core.halo.run/v1alpha1/linkgroups/**',
                    '/apis/api.plugin.halo.run/v1alpha1/plugins/PluginLinks/**'
                ]
            }
        }
        groupedApiMappings = [
                '/v3/api-docs/linksV1alpha1Api': 'linksV1alpha1Api.json'
        ]
        generator {
            outputDir = file("${projectDir}/console/src/api/generated")
        }
    }
}
