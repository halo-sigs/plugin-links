plugins {
    id "com.github.node-gradle.node" version "7.0.2"
    id "run.halo.plugin.devtools" version "0.1.1"
    id "io.freefair.lombok" version "8.0.1"
    id 'java'
}

group 'run.halo.links'
sourceCompatibility = JavaVersion.VERSION_17

repositories {
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
    maven { url 'https://repo.spring.io/milestone' }
    mavenCentral()
}

dependencies {
    implementation platform('run.halo.tools.platform:plugin:2.15.0-SNAPSHOT')
    compileOnly 'run.halo.app:api'

    testImplementation 'run.halo.app:api'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

node {
    nodeProjectDir = file("${project.projectDir}/console")
}

tasks.register('buildFrontend', PnpmTask) {
    args = ['build']
    dependsOn('installDepsForUI')
}

tasks.register('installDepsForUI', PnpmTask) {
    args = ['install']
}

build {
    // build frontend before build
    tasks.named('compileJava').configure {
        dependsOn('buildFrontend')
    }
}

halo {
    version = '2.21.0-alpha.1'
    debug = true
}

haloPlugin {
    openApi {
        outputDir = file("$rootDir/api-docs/openapi/v3_0")
        groupingRules {
            linksV1alpha1Api {
                displayName = 'Extension API for Links'
                pathsToMatch = [
                    '/apis/core.halo.run/v1alpha1/links/**',
                    '/apis/core.halo.run/v1alpha1/linkgroups/**',
                    '/apis/api.plugin.halo.run/v1alpha1/plugins/PluginLinks/**'
                ]
            }
        }
        groupedApiMappings = [
                '/v3/api-docs/linksV1alpha1Api': 'linksV1alpha1Api.json'
        ]
        generator {
            outputDir = file("${projectDir}/console/src/api/generated")
        }
    }
}
